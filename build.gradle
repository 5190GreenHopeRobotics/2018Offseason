import jaci.gradle.deploy.artifact.FileCollectionArtifact
import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
}

def TEAM = 5190
def ROBOT_CLASS = "frc.team5190.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
            user = 'admin'
            password = ''
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }

        artifact('LS-LL', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/resources/Raw JSONs/LS-LL')
            directory = '/home/lvuser/paths/Raw JSONs/LS-LL'
            targets << "roborio"
        }

        artifact('LS-RR', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/resources/Raw JSONs/LS-RR')
            directory = '/home/lvuser/paths/Raw JSONs/LS-RR'
            targets << "roborio"
        }

        artifact('CS-L', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/resources/Raw JSONs/CS-L')
            directory = '/home/lvuser/paths/Raw JSONs/CS-L'
            targets << "roborio"
        }

        artifact('CS-R', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/resources/Raw JSONs/CS-R')
            directory = '/home/lvuser/paths/Raw JSONs/CS-R'
            targets << "roborio"
        }

        artifact('Test', FileCollectionArtifact) {
            files = project.fileTree(dir: 'src/main/resources/Raw JSONs/Test')
            directory = '/home/lvuser/paths/Raw JSONs/Test'
            targets << "roborio"
        }
    }
}

repositories {
    mavenCentral()
}
// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()
    compile pathfinder()

    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}