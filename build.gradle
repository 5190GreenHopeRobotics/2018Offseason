import edu.wpi.first.gradlerio.GradleRIOPlugin
import edu.wpi.first.gradlerio.frc.FRCJavaArtifact
import edu.wpi.first.gradlerio.frc.RoboRIO

/*
 * FRC Team 5190
 * Green Hope Falcons
 */

buildscript {
    ext.kotlin_version = '1.2.71'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

def TEAM = 5190
def ROBOT_CLASS = "org.ghrobotics.robot.Robot"

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kyonifer/maven" }
}


wpi {
    ctreVersion = '5.3.1.0'
}

dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()

    compile project("FalconLibrary")

    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.23.3'
    compile 'org.knowm.xchart:xchart:3.2.2'

    testCompile 'junit:junit:4.12'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}
wrapper {
    gradleVersion = '4.9'
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}